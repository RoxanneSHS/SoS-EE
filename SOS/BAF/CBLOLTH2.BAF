IF
  See([PC.0.0.MAGE])
  OR(10)
    See([PC.0.0.CLERIC])
    See([PC.0.0.FIGHTER_MAGE])
    See([PC.0.0.FIGHTER_CLERIC])
    See([PC.0.0.FIGHTER_MAGE_THIEF])
    See([PC.0.0.MAGE_THIEF])
    See([PC.0.0.CLERIC_MAGE])
    See([PC.0.0.CLERIC_THIEF])
    See([PC.0.0.FIGHTER_MAGE_CLERIC])
    See([PC.0.0.CLERIC_RANGER])
    See([PC.0.0.SORCERER])
  Global("CbLolthOneAppliedProtection","LOCALS",0)
THEN
  RESPONSE #100
    ApplySpell(Myself,WIZARD_SPELL_TURNING)
    SetGlobal("CbLolthOneAppliedProtection","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  See(NearestMyGroupOfType)
  Global("CbLolthTwoBehavior","LOCALS",0)
THEN
  RESPONSE #100
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_DEFENSIVE_HARMONY)
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_STRENGTH_OF_ONE)
    ReallyForceSpell(Myself,CLERIC_CLOAK_OF_FEAR)
    ReallyForceSpell(Myself,CLERIC_BLESS)
    SetGlobal("CbLolthTwoBehavior","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MIRROR_IMAGE)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_MIRROR_IMAGE)
END

IF
  See(NearestEnemyOf(Myself))
  HPPercentLT(Myself,20)
  HaveSpell(CLERIC_HEAL)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_HEAL)
END

IF
  HPPercentLT(Myself,20)
  HaveSpell(CLERIC_CURE_CRITICAL_WOUNDS)
  !HaveSpell(CLERIC_HEAL)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_CURE_CRITICAL_WOUNDS)
END

IF
  HPPercentLT(Myself,30)
  HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
  HPPercentLT(Myself,35)
  HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_CURE_MEDIUM_WOUNDS)
END

IF
  HPPercentLT(Myself,50)
  HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_CURE_LIGHT_WOUNDS)
END

IF
  See(NearestEnemyOf(Myself))
  See(NearestMyGroupOfType)
  HaveSpell(CLERIC_MASS_CURE)
  HPPercentLT(Myself,80)
  OR(3)
    HPPercentLT(LastSeenBy(Myself),80)
    HPPercentLT(SecondNearestMyGroupOfType,80)
    HPPercentLT(ThirdNearestMyGroupOfType,80)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_MASS_CURE)
    Spell(LastSeenBy(Myself),WIZARD_HASTE)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_SILENCE_15_FOOT)
  !Range(NearestEnemyOf(Myself),12)
  See([PC.0.0.CLERIC])
  OR(4)
    See([PC.0.0.MAGE])
    See([PC.0.0.CLERIC_MAGE])
    See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
    See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_SILENCE_15_FOOT)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_DISPEL_MAGIC)
  !Range(NearestEnemyOf(Myself),12)
  See([PC.0.0.MAGE])
  OR(4)
    See([PC.0.0.CLERIC_MAGE])
    Detect([PC.0.0.MAGE])
    Detect([PC.0.0.CLERIC])
    Detect([PC.0.0.CLERIC_MAGE])
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_DISPEL_MAGIC)
END

IF
  See([GOODCUTOFF.0.SKELETON.0.0.SUMMONED])
  HaveSpell(CLERIC_SUNRAY)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_SUNRAY)
END

IF
  See([GOODCUTOFF.0.SKELETON.0.0.SUMMONED])
  !HaveSpell(CLERIC_SUNRAY)
  HaveSpell(CLERIC_REPULSE_UNDEAD)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_REPULSE_UNDEAD)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_BLADE_BARRIER)
  Global("CbLolthsBladeBarrier","LOCALS",0)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
THEN
  RESPONSE #100
    Spell(Myself,NPC_BLADE_BARRIER)
    SetGlobal("CbLolthsBladeBarrier","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_PHYSICAL_MIRROR)
  HPGT(LastSeenBy(Myself),20)
  !HasBounceEffects(Myself)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_PHYSICAL_MIRROR)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_HOLD_PERSON)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(NearestEnemyOf(Myself),STATE_SLEEPING)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_HOLD_PERSON)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_FREE_ACTION)
  See(NearestMyGroupOfType)
  StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_FREE_ACTION)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_HOLY_SMITE)
  Alignment(Myself,MASK_GOOD)
  Global("blightsmite","LOCALS",0)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_HOLY_SMITE)
    SetGlobal("blightsmite","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_UNHOLY_BLIGHT)
  Alignment(Myself,MASK_EVIL)
  Global("blightsmite","LOCALS",0)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_UNHOLY_BLIGHT)
    SetGlobal("blightsmite","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_CHAOTIC_COMMANDS)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_CHAOTIC_COMMANDS)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_CAUSE_CRITICAL_WOUNDS)
  StateCheck(Myself,STATE_MIRRORIMAGE)
THEN
  RESPONSE #100
    ForceSpell(NearestEnemyOf(Myself),WIZARD_DIMENSION_DOOR)
    Spell(Myself,CLERIC_CAUSE_CRITICAL_WOUNDS)
    AttackOneRound(NearestEnemyOf(Myself))
    AttackOneRound(NearestEnemyOf(Myself))
    DestroyItem("serious")  // ~Cause Serious Wounds~
    ForceSpellPoint([3344.1245],WIZARD_DIMENSION_DOOR)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_FLAME_STRIKE)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_FLAME_STRIKE)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_POISON)
  Range(NearestEnemyOf(Myself),3)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_POISON)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_CONJURE_FIRE_ELEMENTAL)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_CONJURE_FIRE_ELEMENTAL)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_CONJURE_EARTH_ELEMENTAL)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_CONJURE_EARTH_ELEMENTAL)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(CLERIC_FINGER_OF_DEATH)
  HPGT(LastSeenBy(Myself),30)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),CLERIC_FINGER_OF_DEATH)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_CONFUSION)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_CONFUSION)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_FIREBALL)
  HPGT(LastSeenBy(Myself),30)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),WIZARD_FIREBALL)
END

IF
  See(NearestEnemyOf(Myself))
  HPGT(LastSeenBy(Myself),20)
  RandomNum(5,1)
  !HasBounceEffects(LastSeenBy(Myself))
  !CheckStatGT(LastSeenBy(Myself),75,RESISTMAGIC)
  HaveSpell(WIZARD_MAGIC_MISSILE)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

IF
  See(NearestEnemyOf(Myself))
  HPGT(LastSeenBy(Myself),20)
  RandomNum(5,2)
  !HasBounceEffects(LastSeenBy(Myself))
  !CheckStatGT(LastSeenBy(Myself),75,RESISTMAGIC)
  HaveSpell(WIZARD_AGANNAZAR_SCORCHER)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_AGANNAZAR_SCORCHER)
END

IF
  See(NearestEnemyOf(Myself))
  HPGT(LastSeenBy(Myself),20)
  RandomNum(5,3)
  !HasBounceEffects(LastSeenBy(Myself))
  !CheckStatGT(LastSeenBy(Myself),75,RESISTMAGIC)
  HaveSpell(WIZARD_CONE_OF_COLD)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_CONE_OF_COLD)
END

IF
  See(NearestEnemyOf(Myself))
  HPGT(LastSeenBy(Myself),20)
  RandomNum(5,4)
  !HasBounceEffects(LastSeenBy(Myself))
  !CheckStatGT(LastSeenBy(Myself),75,RESISTMAGIC)
  HaveSpell(WIZARD_STINKING_CLOUD)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_STINKING_CLOUD)
END

IF
  See(NearestEnemyOf(Myself))
  HPGT(LastSeenBy(Myself),20)
  RandomNum(5,5)
  !HasBounceEffects(LastSeenBy(Myself))
  !CheckStatGT(LastSeenBy(Myself),75,RESISTMAGIC)
  HaveSpell(WIZARD_LIGHTNING_BOLT)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_LIGHTNING_BOLT)
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackReevaluate(NearestEnemyOf(Myself),11)
END

